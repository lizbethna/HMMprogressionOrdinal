/*
A hidden Markov model to address measurement errors in ordinal response scale and non-decreasing process.

Lizbeth Naranjo, Luz Judith R. Esparza, and Carlos J. Perez

SIMULATE DATA
*/
 
### LIKELIHOOD
model{

	for(i in 1:N){		

### First examination, prevalence
		Yobs[i,1] ~ dinterval(Wobs[i,1],kappa[1:3])
		Wobs[i,1] ~ dnorm(Wtrue[i,1],Tau[Xi[i]])
		Wtrue[i,1] ~ dnorm(ETA[i,1],1)		
		
		ETA[i,1] <- X[i,1]*Beta[1] + X[i,2]*Beta[2] + Z[i,1,1]*Gama[1] + Z[i,1,2]*Gama[2] 
	
		
### 2nd,3rd,...,Kth examination, incidence
		for(k in 2:K){				

			Yobs[i,k] ~ dinterval(Wobs[i,k],kappa[1:3])	
			Wobs[i,k] ~ dnorm(Wtrue[i,k],Tau[Xi[i]])
			Wtrue[i,k] ~ dnorm(ETA[i,k],1)T(Wtrue[i,k-1],)

			ETA[i,k] <- X[i,1]*Beta[1] + X[i,2]*Beta[2] + Z[i,k,1]*Gama[1] + Z[i,k,2]*Gama[2] 

		}
	}

### PRIOR
	Beta[1] ~ dnorm(0.0,1.0E-4)
	Beta[2] ~ dnorm(0.0,1.0E-4)
	Gama[1] ~ dnorm(0.0,1.0E-4)
	Gama[2] ~ dnorm(0.0,1.0E-4)

	Tau[1] ~ dgamma(0.01,0.01)
	Tau[2] ~ dgamma(0.01,0.01)
	Sig2[1] <- 1/Tau[1] 
  	Sig2[2] <- 1/Tau[2] 
	Sig[1] <- sqrt(Sig2[1])
	Sig[2] <- sqrt(Sig2[2])

	gam[1] ~ dnorm(0.0,1.0E-4)
	gam[2] ~ dnorm(0.0,1.0E-4)
	gam[3] ~ dnorm(0.0,1.0E-4)
	kappa0[1:3] <- sort(gam[1:3])
	kappa <- kappa0-gam[1] 

}
